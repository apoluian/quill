apply plugin: 'groovy'
apply plugin: 'maven'

group = 'org.myire'
version = '1.4-dev.1'

repositories
{
    mavenCentral()
}

ext.mavenDependencyVersion = '3.3.9'

dependencies
{
    compile gradleApi()

    compile 'org.myire:scent:0.9'

    compile "org.apache.maven:maven-core:${mavenDependencyVersion}@jar"
    compile "org.apache.maven:maven-builder-support:${mavenDependencyVersion}@jar"
    compile "org.apache.maven:maven-embedder:${mavenDependencyVersion}@jar"
    compile "org.apache.maven:maven-model-builder:${mavenDependencyVersion}@jar"
    compile "org.apache.maven:maven-settings-builder:${mavenDependencyVersion}@jar"
    compile 'org.codehaus.plexus:plexus-classworlds:2.5.2@jar'
    compile 'org.eclipse.aether:aether-api:1.1.0@jar'
    compile 'org.eclipse.sisu:org.eclipse.sisu.plexus:0.3.3@jar'

    testCompile 'junit:junit:4.12'
}

ext.sourceAndTargetCompatibility = 1.7

tasks.withType(GroovyCompile)
{
    sourceCompatibility = sourceAndTargetCompatibility
    targetCompatibility = sourceAndTargetCompatibility
}

tasks.withType(JavaCompile)
{
    sourceCompatibility = sourceAndTargetCompatibility
    targetCompatibility = sourceAndTargetCompatibility
}

task ('sourcesJar', type: Jar)
{
    classifier = 'sources'
    from sourceSets.main.allSource
}

task ('groovydocJar', type: Jar, dependsOn: groovydoc)
{
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}

task ('emptyJavadocJar', type: Jar)
{
    classifier = 'javadoc'
    from fileTree(dir:'meta', includes:['README-empty-javadoc.txt'])
}

task ('createPom')
{
    def pomFile = "$mavenPomDir/${project.archivesBaseName}-${project.version}.pom"
    outputs.file(pomFile)

    def pom = pom()
    pom.withXml
    {
        def pomRoot = asNode()
        def dependencies = pomRoot.get('dependencies')
        if (dependencies)
            pomRoot.remove(dependencies)

        new XmlParser().parse(file('meta/pom-template.xml')).children().each
        {
            pomRoot.append(it)
        }
    }

    doFirst { pom.writeTo(pomFile) }
}

build.finalizedBy 'sourcesJar', 'groovydocJar', 'emptyJavadocJar', 'createPom'
